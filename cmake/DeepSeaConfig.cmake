if (CMAKE_VERSION VERSION_LESS 3.0.2)
	message(FATAL_ERROR "DeepSea requires at least CMake version 3.0.2")
endif()

if (NOT DeepSea_FIND_COMPONENTS)
	set(DeepSea_NOT_FOUND_MESSAGE "The DeepSea package requires at least one component")
	set(DeepSea_FOUND False)
	return()
endif()

get_filename_component(_DeepSea_InstallPrefix "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)

include(CMakeFindDependencyMacro)
set(_DeepSea_NOTFOUND_MESSAGE)
foreach(module ${DeepSea_FIND_COMPONENTS})
	find_dependency(DeepSea${module})
	if (NOT DeepSea${module}_FOUND)
		if (DeepSea_FIND_REQUIRED_${module})
			set(_DeepSea_NOTFOUND_MESSAGE "${_DeepSea_NOTFOUND_MESSAGE}Failed to find DeepSea component \"${module}\" config file at \"${_DeepSea_InstallPrefix}/DeepSea${module}/DeepSea${module}Config.cmake\"\n")
		elseif(NOT DeepSea_FIND_QUIETLY)
			message(WARNING "Failed to find DeepSea component \"${module}\" config file at \"${_DeepSea_InstallPrefix}/DeepSea${module}/DeepSea${module}Config.cmake\"")
		endif()
	endif()
endforeach()

if (_DeepSea_NOTFOUND_MESSAGE)
	set(DeepSea_NOT_FOUND_MESSAGE "${_DeepSea_NOTFOUND_MESSAGE}")
	set(DeepSea_FOUND False)
endif()
