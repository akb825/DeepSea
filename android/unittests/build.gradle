apply plugin: 'com.android.application'

android {
    compileSdk 34
    ndkVersion "26.3.11579264"
    defaultConfig {
        applicationId "com.akb825.deepsea.unittest"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            ndk {
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true
        }
    }
    externalNativeBuild {
        cmake {
            path "${projectDir}/../../CMakeLists.txt"
        }
    }

    flavorDimensions += "unittest"
    productFlavors {
        CoreUnitTests {
            externalNativeBuild {
                cmake {
                    targets "deepsea_core_test"
                }
            }
            dimension "unittest"

            applicationIdSuffix ".core"
            versionNameSuffix "-Core"
            resValue "string", "app_name", "CoreUnitTests"
            resValue "string", "main_lib_name", "deepsea_core_test"
        }

        RenderBootstrapUnitTests {
            externalNativeBuild {
                cmake {
                    targets "deepsea_render_bootstrap_test"
                }
            }
            dimension "unittest"

            applicationIdSuffix ".render_bootstrap"
            versionNameSuffix "-RenderBootstrap"
            resValue "string", "app_name", "RenderBootstrapUnitTests"
            resValue "string", "main_lib_name", "deepsea_render_bootstrap_test"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.akb825.deepsea.unittests'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}
