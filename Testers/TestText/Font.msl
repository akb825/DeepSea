/*
 * Copyright 2017 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#include <DeepSea/Render/Shaders/VertexAttributes.mslh>
#include <DeepSea/Text/Text.mslh>

uniform sampler2D fontTex;
sampler_state fontTex
{
	address_mode_u = clamp_to_edge;
	address_mode_v = clamp_to_edge;
	min_filter = linear;
	mag_filter = linear;
	mip_filter = none;
};

uniform SharedInfo
{
	uvec2 screenSize;
} sharedInfo;

uniform vec2 position;

[[vertex]] layout(location = DS_POSITION) in vec2 vertexPos;
[[vertex]] layout(location = DS_COLOR0) in vec4 textColor;
[[vertex]] layout(location = DS_COLOR1) in vec4 outlineColor;
[[vertex]] layout(location = DS_TEXCOORD0) in vec3 texCoords;
[[vertex]] layout(location = DS_TEXCOORD1) in vec3 style;

struct VertexOut
{
	vec4 textColor;
	vec4 outlineColor;
	vec3 texCoords;
	vec3 style;
};

[[vertex]] out VertexOut vertexOut;

[[fragment]] in VertexOut vertexOut;
[[fragment]] out vec4 color;

[[vertex]]
void vertexShader()
{
	vertexOut.textColor = textColor;
	vertexOut.outlineColor = outlineColor;
	vertexOut.texCoords = texCoords;
	vertexOut.style = style;

	vec2 normalizedPos = (uniforms.position + vertexPos)/vec2(INSTANCE(sharedInfo).screenSize);
	gl_Position = DS_ADJUST_CLIP(vec4(normalizedPos*vec2(2.0) - vec2(1.0), 0.0, 1.0));
	gl_Position.y = -gl_Position.y;
}

[[fragment]]
void fragmentShader()
{
	float distance = textureLod(fontTex, vertexOut.texCoords.xy, vertexOut.texCoords.z).r;
	color = computeTextColor(distance, vertexOut.style.x, vertexOut.style.y, vertexOut.style.z,
		vertexOut.textColor, vertexOut.outlineColor);
}

pipeline Font
{
	vertex = vertexShader;
	fragment = fragmentShader;

	blend_enable = true;
	src_color_blend_factor = src_alpha;
	src_alpha_blend_factor = one;
	dst_blend_factor = one_minus_src_alpha;
	alpha_blend_op = add;
}
