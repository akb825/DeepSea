/*
 * Copyright 2017-2025 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#pragma once

/**
 * @brief Scale to apply to parameters such as embolding and outline thickness to ensure a
 * reasonable amount in the range [0, 1] based on the window size for the distance field.
 */
#define DS_TEXT_THICKNESS_SCALE 0.5

/**
 * @brief Computes the signed distance from a texture.
 * @param fontTex The sampler for the font texture.
 * @param texCoords The texture coordinates for the current position. The z value is the mip level.
 * @param mipLevel The mip level that the glyph is stored in.
 * @return The signed distance represented in the range [0, 1].
 */
lowp float dsComputeTextDistance(lowp sampler2D fontTex, vec3 texCoords)
{
	return textureLod(fontTex, texCoords.xy, texCoords.z).r;
}

/**
 * @brief Computes the text color based on a signed distance field.
 * @param distance The signed distance represented in the range [0, 1], typically cpmputed from
 *     dsComputeTextDistance().
 * @param embolden The amount to embolden the text.
 * @param antiAlias The amount to anti-alias the text.
 * @param textColor The color of the text.
 * @return The text color. This uses pre-multiplied alpha.
 */
lowp vec4 dsComputeTextColor(
	lowp float distance, lowp float embolden, lowp float antiAlias, lowp vec4 textColor)
{
	textColor.rgb *= textColor.a;
	lowp float insideThreshold = -0.5*DS_TEXT_THICKNESS_SCALE*embolden + 0.5;
	lowp float inside = smoothstep(
		insideThreshold - antiAlias, insideThreshold + antiAlias, distance);
	return textColor*inside;
}

/**
 * @brief Computes the text color based on a signed distance field.
 * @param distance The signed distance represented in the range [0, 1], typically cpmputed from
 *     dsComputeTextDistance().
 * @param embolden The amount to embolden the text.
 * @param antiAlias The amount to anti-alias the text.
 * @param outlinePosition The position of the outline in the range [-1, 1], where 0 is on the text
 *     boundary and 1 is further from the center.
 * @param outlineThickness The thickness of the outline in the range [0, 1].
 * @param textColor The color of the text.
 * @param outlineColor The color of the text outline.
 * @return The text color. This uses pre-multiplied alpha.
 */
lowp vec4 dsComputeTextColor(lowp float distance, lowp float embolden, lowp float outlinePosition,
	lowp float outlineThickness, lowp float antiAlias, lowp vec4 textColor, lowp vec4 outlineColor)
{
	textColor.rgb *= textColor.a;
	lowp float insideThreshold = -0.5*DS_TEXT_THICKNESS_SCALE*embolden + 0.5;
	lowp float inside = smoothstep(
		insideThreshold - antiAlias, insideThreshold + antiAlias, distance);
	lowp vec4 color = textColor;

	if (outlineThickness > 0.0 && outlineColor.a > 0)
	{
		outlineColor.rgb *= outlineColor.a;
		lowp vec2 outlineThreshold =
			vec2(outlinePosition) + vec2(outlineThickness, -outlineThickness);
		outlineThreshold = -0.5*DS_TEXT_THICKNESS_SCALE*outlineThreshold + vec2(0.5);

		lowp float insideMinOutline = smoothstep(outlineThreshold.x - antiAlias,
			outlineThreshold.x + antiAlias, distance);
		lowp float insideMaxOutline = 1.0 - smoothstep(outlineThreshold.y - antiAlias,
			outlineThreshold.y + antiAlias, distance);
		lowp float insideOutline = insideMinOutline*insideMaxOutline;
		color = mix(color, outlineColor, 1.0 - inside);
		color = mix(color, outlineColor, insideOutline);
		inside = mix(inside, 1.0, insideOutline);
	}

	return color*inside;
}
