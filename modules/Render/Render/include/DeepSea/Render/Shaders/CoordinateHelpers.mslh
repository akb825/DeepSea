/*
 * Copyright 2022 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#pragma once

/**
 * @file
 * @brief Functions to help with different coordinate transformations.
 */

/**
 * @brief Transforms from clip coordinates in the common [-1, -1] as lower left to texture
 * coordinates.
 *
 * This is typically used for cases where the clip coordinates are passed directly as vertex
 * coordinates and avoids calling DS_ADJUST_DIRECT_CLIP() to handle APIs where [-1, -1] is the
 * upper left.
 *
 * @param clipCoords The coordinates in clip space.
 * @return The texture coordinates.
 */
vec2 dsCommonClipToTexCoords(vec2 clipCoords)
{
	return vec2(clipCoords.x, -clipCoords.y)*vec2(0.5) + vec2(0.5);
}

/**
 * @brief Transforms from clip coordinates to texture coordinates.
 * @param clipCoords The coordinates in clip space.
 * @return The texture coordinates.
 */
vec2 dsClipToTexCoords(vec2 clipCoords)
{
#if SPIRV_VERSION
	// Avoid having to call DS_ADJUST_DIRECT_CLIP and have duplicate operations.
	return clipCoords*vec2(0.5) + vec2(0.5);
#else
	return vec2(clipCoords.x, -clipCoords.y)*vec2(0.5) + vec2(0.5);
#endif
}

/**
 * @brief Swizzles the work group ID for compute shaders to better handle the texture cache for
 *     image resolves.
 * @remark This assumes a 2D workgroup, i.e. gl_NumWorkGroups.z is 1.
 * @param limit The limit across which to swizzle.
 * @return The swizzled ID.
 */
[[compute]]
uvec2 dsSwizzleWorkGroupID(uint limit)
{
	uint columnSize = gl_NumWorkGroups.y*min(limit, gl_NumWorkGroups.x);

	uint globalIndex = gl_WorkGroupID.y*gl_NumWorkGroups.x + gl_WorkGroupID.x;
	uint columnIndex = globalIndex/columnSize;
	uint columnRem = globalIndex % columnSize;

	uint baseX = columnIndex*limit;
	uint columnWidth = min(limit, gl_NumWorkGroups.x - baseX);
	return uvec2(baseX + columnRem % columnWidth, columnRem/columnWidth);
}

/**
 * @brief Swizzles the global invocation ID for compute shaders to better handle the texture cache
 *     for image resolves.
 * @remark This assumes a 2D workgroup, i.e. gl_NumWorkGroups.z is 1.
 * @param limit The limit across which to swizzle.
 * @param workGroupSize The work group size. This is passed as a parameter since gl_WorkGroupSize
 *     will resolve to (1, 1, 1) if defined before the special in variable.
 * @return The swizzled ID.
 */
[[compute]]
uvec2 dsSwizzleGlobalInvocationID(uint limit, uvec2 workGroupSize)
{
	return dsSwizzleWorkGroupID(limit)*workGroupSize + gl_LocalInvocationID.xy;
}
