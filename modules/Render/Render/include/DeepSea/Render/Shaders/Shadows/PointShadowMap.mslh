/*
 * Copyright 2021 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#pragma once

/**
 * @file
 * @brief Function to compute a point light shadow map value.
 *
 * This expects DS_SHADOW_FILTER to be defined for the shadow filter function. This typically means
 * including an appropriate shadow filter header (e.g. ShadowPCF2x2.mslh) before including this
 * file.
 */

#if !HAS_SHADOW_SAMPLERS || !HAS_TEXTURE_ARRAYS
#error Point light shadows require support for shadow samplers and texture arrays
#endif

#ifndef DS_SHADOW_FILTER
#error DS_SHADOW_FILTER must be defined to provide the shadow filter function.
#endif

/**
 * @brief Performs a shadow map lookup.
 * @param shadowMap The shadow map to look up in.
 * @param pointProjections The point projection matrices.
 * @param viewPos The view position.
 * @param lightViewPos The view position of the light.
 * @param fadeDistances The distances to start and stop fading shadows.
 * @param texCoordTransform The scale and offset to apply to the shadow projection to convert to
 *     texture coordinates.
 * @return A value of 0 to 1, where 0 is fully shadowed and 1 is fully lit.
 */
[[fragment]]
float dsPointShadowMap(sampler2DArrayShadow shadowMap, mat4 pointProjections[6], vec3 viewPos,
	vec3 lightViewPos, vec2 fadeDistances, vec3 texCoordTransform[2])
{
	float viewZ = -viewPos.z;
	if (viewZ >= fadeDistances.y)
		return 1.0;

	float fadeRange = fadeDistances.y - fadeDistances.x;
	float fadeAmount = 0.0;
	if (fadeRange > 1e-3)
		fadeAmount = max(viewZ - fadeDistances.x, 0.0)/fadeRange;

	vec3 lightToPos = viewPos - lightViewPos;
	vec3 absLightToPos = abs(lightToPos);

	int surface;
	if (absLightToPos.x > absLightToPos.y && absLightToPos.x > absLightToPos.z)
		surface = lightToPos.x > 0 ? 0 : 1;
	else if (absLightToPos.y > absLightToPos.x && absLightToPos.y > absLightToPos.z)
		surface = lightToPos.y > 0 ? 2 : 3;
	else
		surface = lightToPos.z > 0 ? 4 : 5;

	vec4 shadowPos = DS_ADJUST_CLIP(pointProjections[surface]*vec4(viewPos, 1.0));
	shadowPos /= shadowPos.w;
	vec3 shadowTexCoords = shadowPos.xyz*texCoordTransform[0] + texCoordTransform[1];
	shadowTexCoords.z = clamp(shadowTexCoords.z, 0.0, 1.0);
	float shadowVal = DS_SHADOW_FILTER(shadowMap, surface, shadowTexCoords);
	return mix(shadowVal, 1.0, fadeAmount);
}
