/*
 * Copyright 2021 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#pragma once

/**
 * @file
 * @brief Function to compute a cascaded shadow map value.
 *
 * This expects DS_SHADOW_FILTER to be defined for the shadow filter function. This typically means
 * including an appropriate shadow filter header (e.g. ShadowPCF2x2.mslh) before including this
 * file.
 */

#if !HAS_SHADOW_SAMPLERS || !HAS_TEXTURE_ARRAYS
#error Cascaded shadows require support for shadow samplers and texture arrays
#endif

#ifndef DS_SHADOW_FILTER
#error DS_SHADOW_FILTER must be defined to provide the shadow filter function.
#endif

/**
 * @brief Performs a shadow map lookup.
 * @param shadowMap The shadow map to look up in.
 * @param cascadeProjections The cascaded projection matrices.
 * @param splitDistances The distances for each cascade split.
 * @param viewPos The view position.
 * @param fadeStart The distance from the view point to start fading shadows.
 * @param fadeEnd The distance from the view point to stop fading shadows.
 * @return A value of 0 to 1, where 0 is fully shadowed and 1 is fully lit.
 */
[[fragment]]
float dsCascadedShadowMap(sampler2DArrayShadow shadowMap, mat4 cascadeProjections[4],
	vec4 splitDistances, vec3 viewPos, float fadeStart, float fadeEnd)
{
	float viewZ = -viewPos.z;
	ivec4 beyondSplit = ivec4(greaterThan(vec4(viewZ), splitDistances));
	int split = byeondSplit.x + byeondSplit.y + byeondSplit.z + byeondSplit.w;

	// Regardless of the number of splits, the split index will be 0 if fully out of the range of
	// shadows. (since all following splits are set to far plane)
	if (split >= 4 || viewZ >= fadeEnd)
		return 1.0;

	float fadeRange = fadeEnd - fadeStart;
	float fadeAmount = 0.0;
	if (fadeRange > 1e-3)
		fadeAmount = max(viewZ - fadeStart, 0.0)/fadeRange;

	vec4 shadowPos = DS_ADJUST_CLIP(cascadeProjections[split]*vec4(viewPos, 1.0));
	shadowPos /= shadowPos.w;
	shadowPos.xy = shadowPos.xy*vec2(0.5) + vec2(0.5);
	if (DS_CLIP_HALF_DEPTH == 0)
		shadowPos.z = shadowPos.z*0.5 + 0.5;
	shadowPos.z = clamp(shadowPos.z, 0.0, 1.0);
	float shadowVal = DS_SHADOW_FILTER(shadowMap, split, shadowPos);
	return mix(shadowVal, 1.0, fadeAmount);
}
