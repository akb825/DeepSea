/*
 * Copyright 2018 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#pragma once

#include <DeepSea/Render/Shaders/Color.mslh>
#include <DeepSea/Render/Shaders/VertexAttributes.mslh>
#include <DeepSea/VectorDraw/Shaders/VectorShared.mslh>
#include <DeepSea/VectorDraw/Shaders/VectorMaterial.mslh>

/**
 * @file
 * @brief Shader implementation for vector line elements.
 *
 * These provide the vertex elements, uniforms, and basic vertex and pixel inputs/outputs.
 * You may write your own functions to incorperate them plus additional functionality, or simply
 * use the defaults.
 *
 * Shaders must provide a pipeline of the name dsVectorLine. For example, the following pipeline
 * definition will provide the default implementation:
 *
 * ```
 * pipeline dsVectorLine
 * {
 *     vertex = dsVectorElementLine_defaultVertex;
 *     fragment = dsVectorElementLine_defaultFragment;
 *     DS_DEFAULT_VECTOR_ELEMENT_LINE_RENDER_STATES;
 * }
 * ```
 *
 * dsVectorElementLine_defaultFragmentSRGB is provided to convert the final color to sRGB space.
 * However, in some situations it's still preferable to leave the color in linear space when doing
 * sRGB-correct rendering:
 * - When writing to an sRGB framebuffer, where the hardware will do the conversion.
 * - When blending with linear framebuffers. It is recommended that high-precision (> 8 bits per
 *   channel) is used in this case to avoid precision loss.
 */

/**
 * @brief The position of the vertex.
 *
 * The zw coordinates are the distance along the line and total line distance. This will be < 0
 * when not a part of a stroke.
 */
[[vertex]] layout(location = DS_POSITION) in vec4 dsVectorPositionLineInfo;

/**
 * @brief The indices within the material and shape.
 */
[[vertex]] layout(location = DS_TEXCOORD0) in vec2 dsVectorElementMaterialIndex;

/**
 * @brief Output from the vertex stage.
 */
struct dsVectorVertexOutput
{
	/**
	 * @brief The original and image position of the vertex.
	 */
	vec4 position;

	/**
	 * @brief Current and maximum distance in the line, or -1 if not a line, opacity, and material
	 * index.
	 */
	vec4 lineDistancesOpacityMaterialIndex;

	/**
	 * @brief The bounds of the shape.
	 */
	vec4 shapeBounds;

	/**
	 * @brief The information for the dashed line.
	 */
	vec4 dashArray;
};

/**
 * @brief The vertex output.
 */
[[vertex]] out dsVectorVertexOutput dsVectorVertexOut;
[[vertex]] out dsVectorMaterialInfo dsVectorVertexMaterialOut;
[[fragment]] in dsVectorVertexOutput dsVectorVertexOut;
[[fragment]] in dsVectorMaterialInfo dsVectorVertexMaterialOut;

/**
 * @brief The final output color of the shader.
 */
[[fragment]] out vec4 dsVectorFinalColor;

/**
 * @brief Default render states for vector line elements.
 */
#define DS_DEFAULT_VECTOR_ELEMENT_LINE_RENDER_STATES \
	cull_mode = back; \
	blend_enable = true; \
	src_color_blend_factor = src_alpha; \
	src_alpha_blend_factor = one; \
	dst_blend_factor = one_minus_src_alpha; \
	alpha_blend_op = add

/**
 * @brief Creates the vertex output.
 * @param[out] outPosition The output position in clip space.
 * @return The vertex output.
 */
[[vertex]]
dsVectorVertexOutput dsVectorElementLine_processVertex(out vec4 outPosition)
{
	vec4 elementInfo[4];
	dsVectorShared_getElementLineInfo(elementInfo, dsVectorElementMaterialIndex.x);
	mat3 transform = dsVectorShared_getElementTransform(elementInfo);

	dsVectorVertexOutput result;
	result.position.xy = dsVectorPositionLineInfo.xy;
	outPosition = dsVectorShared_transform(result.position.zw, dsVectorPositionLineInfo.xy,
		transform);
	result.lineDistancesOpacityMaterialIndex = vec4(dsVectorPositionLineInfo.zw, elementInfo[2].z,
		dsVectorElementMaterialIndex.y);
	result.shapeBounds = dsVectorShared_getElementBounds(elementInfo);
	result.dashArray = elementInfo[3];

	return result;
}

/**
 * @brief Default vertex shader entry point function.
 */
[[vertex]]
void dsVectorElementLine_defaultVertex()
{
	dsVectorVertexOut = dsVectorElementLine_processVertex(gl_Position);
	dsVectorVertexMaterialOut = dsVectorMaterial_getInfo(dsVectorElementMaterialIndex.y);
	gl_Position = DS_ADJUST_CLIP(gl_Position);
}

/**
 * @brief Gets the material color of the shape.
 * @return The material color.
 */
[[fragment]]
vec4 dsVectorElementLine_getMaterialColor()
{
	dsVectorShared_clip(dsVectorVertexOut.position.zw);

	// Discard gaps for line dashing.
	if (dsVectorVertexOut.lineDistancesOpacityMaterialIndex.x >= 0.0)
	{
		vec4 dashArray = dsVectorVertexOut.dashArray;
		vec4 dashArrayCumalitive;
		dashArrayCumalitive.x = dashArray.x;
		dashArrayCumalitive.y = dashArrayCumalitive.x + dashArray.y;
		dashArrayCumalitive.z = dashArrayCumalitive.y + dashArray.z;
		dashArrayCumalitive.w = dashArrayCumalitive.z + dashArray.w;
		float totalDashDistance = dashArrayCumalitive.w;
		if (totalDashDistance > 0.0)
		{
			// Amortize the remaining dash distance if the error is within 10% of the original dash
			// distance. This should make use cases like loops with dashed arrays easier to look
			// correct.
			float dashRepeats = dsVectorVertexOut.lineDistancesOpacityMaterialIndex.y/
				totalDashDistance;
			float totalDashRepeats = ceil(dashRepeats);
			float dashRepeatRem = totalDashRepeats*totalDashDistance -
				dsVectorVertexOut.lineDistancesOpacityMaterialIndex.y;
			if (totalDashRepeats > 1.0 && dashRepeatRem > totalDashDistance*0.5)
			{
				// Reduce the distance if only slighly over.
				totalDashRepeats -= 1.0;
			}

			float adjustedTotalDashDistance =
				dsVectorVertexOut.lineDistancesOpacityMaterialIndex.y/totalDashRepeats;
			if (abs(adjustedTotalDashDistance/totalDashDistance - 1.0) <= 0.1)
			{
				float ratio = adjustedTotalDashDistance/totalDashDistance;
				totalDashDistance = adjustedTotalDashDistance;
				dashArray *= ratio;
				dashArrayCumalitive *= ratio;
			}

			float dashPos = mod(dsVectorVertexOut.lineDistancesOpacityMaterialIndex.x,
				totalDashDistance);
			if ((dashArray.y > 0 &&
					dashPos >= dashArrayCumalitive.x && dashPos <= dashArrayCumalitive.y) ||
				(dashArray.w > 0 &&
					dashPos >= dashArrayCumalitive.z && dashPos <= dashArrayCumalitive.w))
			{
				discard;
			}
		}
	}

	vec4 color = dsVectorMaterial_getColor(dsVectorVertexMaterialOut,
		dsVectorVertexOut.position.xy, dsVectorVertexOut.shapeBounds,
		dsVectorVertexOut.lineDistancesOpacityMaterialIndex.w);
	return color*vec4(1.0, 1.0, 1.0, dsVectorVertexOut.lineDistancesOpacityMaterialIndex.z);
}

/**
 * @brief Default fragment shader entry point function.
 */
[[fragment]]
void dsVectorElementLine_defaultFragment()
{
	dsVectorFinalColor = dsVectorElementLine_getMaterialColor();
}

/**
 * @brief Default fragment shader entry point function that converts to sRGB.
 */
[[fragment]]
void dsVectorElementLine_defaultFragmentSRGB()
{
	dsVectorFinalColor = dsVectorElementLine_getMaterialColor();
	dsVectorFinalColor.rgb = dsSRGBFromLinear(dsVectorFinalColor.rgb);
}
