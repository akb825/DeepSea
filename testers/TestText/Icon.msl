/*
 * Copyright 2025 Aaron Barany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#include <DeepSea/Render/Shaders/VertexAttributes.mslh>
#include <DeepSea/Text/TextureTextIcon.mslh>

uniform SharedInfo
{
	ivec2 screenSize;
	vec2 position;
	float yMult;
} sharedInfo;

[[vertex]] layout(location = DS_POSITION) in vec2 vertexPos;
[[vertex]] layout(location = DS_TEXCOORD0) in vec2 texCoords;

varying(vertex, fragment)
{
	vec2 vfTexCoords;
}

[[fragment]] out vec4 color;

[[vertex]]
void vertexShader()
{
	vfTexCoords = texCoords;

	vec2 position = dsComputeTextureTextIconPosition(vertexPos);
	vec2 normalizedPos = (INSTANCE(sharedInfo).position + position)/
		vec2(INSTANCE(sharedInfo).screenSize);
	gl_Position = DS_ADJUST_CLIP(vec4(normalizedPos*vec2(2.0) - vec2(1.0), 0.0, 1.0));
	gl_Position.y *= INSTANCE(sharedInfo).yMult;
}

[[fragment]]
void fragmentShader()
{
	color = texture(TextIconTex, vfTexCoords);
}

pipeline Icon
{
	vertex = vertexShader;
	fragment = fragmentShader;

	blend_enable = true;
	// Pre-multiply alpha.
	src_blend_factor = one;
	dst_blend_factor = one_minus_src_alpha;
	blend_op = add;
	cull_mode = back;
}
